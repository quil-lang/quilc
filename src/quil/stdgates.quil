### Quil standard gate defintions.

## Pauli Gates

DEFGATE I:
    1, 0
    0, 1

DEFGATE X:
    0, 1
    1, 0

DEFGATE Y:
    0, -i
    i, 0

DEFGATE Z:
    1, 0
    0, -1


## Hadamard Gate

DEFGATE H:
    1/sqrt(2), 1/sqrt(2)
    1/sqrt(2), -1/sqrt(2)


## Cartesian Rotation Gates

DEFGATE RX(%theta) p AS PAULI-SUM:
    X(%theta/2) p

DEFGATE RY(%theta) p AS PAULI-SUM:
    Y(%theta/2) p

DEFGATE RZ(%theta) p AS PAULI-SUM:
    Z(%theta/2) p


## Controlled-NOT Variants

DEFGATE CNOT p q AS SEQUENCE:
    CONTROLLED X p q

# Also known as the Toffoli gate.
DEFGATE CCNOT p q r AS SEQUENCE:
    CONTROLLED CONTROLLED X p q r

## Phase Gates

# Equivalent to RZ(pi/2) up to global phase
DEFGATE S:
    1, 0
    0, i

# Equivalent to RZ(pi/4) up to global phase
DEFGATE T:
    1, 0
    0, cis(pi/4)

# Equivalent to RZ(%alpha) up to global phase
DEFGATE PHASE(%alpha):
    1, 0
    0, cis(%alpha)

DEFGATE CPHASE(%alpha) p q AS SEQUENCE:
    CONTROLLED PHASE(%alpha) p q

DEFGATE CPHASE00(%alpha) p q AS SEQUENCE:
    X p
    X q
    CONTROLLED PHASE(%alpha) p q
    X p
    X q

DEFGATE CPHASE01(%alpha) p q AS SEQUENCE:
    X p
    CONTROLLED PHASE(%alpha) p q
    X p

DEFGATE CPHASE10(%alpha) p q AS SEQUENCE:
    X q
    CONTROLLED PHASE(%alpha) p q
    X q

DEFGATE CZ p q AS SEQUENCE:
    CONTROLLED Z p q

## Swap Gates

DEFGATE SWAP AS PERMUTATION:
    0, 2, 1, 3

# Also known as the Fredkin gate.
DEFGATE CSWAP p q r AS SEQUENCE:
    CONTROLLED SWAP p q r

DEFGATE ISWAP p q AS PAULI-SUM:
    XX(pi/4) p q
    YY(pi/4) p q

DEFGATE PSWAP(%theta):
    1, 0,           0,           0
    0, 0,           cis(%theta), 0
    0, cis(%theta), 0,           0
    0, 0,           0,           1

# Lesser-known standard gates
DEFGATE PISWAP(%theta):
    1, 0,               0,               0
    0, cos(%theta/2),   i*sin(%theta/2), 0
    0, i*sin(%theta/2), cos(%theta/2),   0
    0, 0,               0,               1

# there has been some internal debate about whether this gate should be parameterized
# by -1.0*%theta instead. we seem to have picked this one for good, but it's still
# not 100% clear to me that this is the "right" move.
DEFGATE XY(%theta):
    1, 0,               0,               0
    0, cos(%theta/2),   i*sin(%theta/2), 0
    0, i*sin(%theta/2), cos(%theta/2),   0
    0, 0,               0,               1

DEFGATE CAN(%alpha, %beta, %gamma) p q AS PAULI-SUM:
    XX(%alpha/2) p q
    YY(%beta/2) p q
    ZZ(%gamma/2) p q

DEFGATE BLOCH(%alpha, %beta, %gamma) q AS PAULI-SUM:
    X(%alpha) q
    Y(%beta) q
    Z(%gamma) q
